plugins {
    id 'net.minecraftforge.gradle'
    id 'eclipse'
    id 'org.spongepowered.mixin'
}

version = "${mc_version}-${build_number}-FORGE"
if (System.getenv().RELEASE_MODE != "1") {
    version += '-SNAPSHOT'
}

mixin {
    add sourceSets.main, "patchouli.refmap.json"

    config "patchouli_xplat.mixins.json"
    // config "patchouli_forge.mixins.json"
}

repositories {
    maven {
        name = "JEI"
        url "https://maven.blamejared.com/"
        content {
            includeGroup "mezz.jei"
        }
    }
}

minecraft {
    mappings channel: 'official', version: mc_version

    runs {
        client {
            workingDirectory project.file('../run')
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"
            args "-mixin.config=patchouli_xplat.mixins.json"
            mods {
                patchouli {
                    source sourceSets.main
                    source project(":Xplat").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('../run')
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"
            args "-mixin.config=patchouli_xplat.mixins.json"
            mods {
                patchouli {
                    source sourceSets.main
                    source project(":Xplat").sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-47.1.3"
    implementation project(":Xplat")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:15.2.0.27")
}

compileJava {
    source(project(":Xplat").sourceSets.main.allSource)
}

sourcesJar {
    from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${mod_name}",
                "Specification-Vendor": "vazkii",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "${mod_name}",
                "Implementation-Version": "${archiveVersion.get()}",
                "Implementation-Vendor" :"vazkii"
        ])
    }
    finalizedBy 'reobfJar'
}

task apiJar(type: Jar, dependsOn: classes) {
    // Sources included because of MinecraftForge/ForgeGradle#369
    from project(":Xplat").sourceSets.main.allJava
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)

    include 'vazkii/patchouli/api/**'

    archiveClassifier = 'api'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact sourcesJar
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
