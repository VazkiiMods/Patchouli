plugins {
	id 'fabric-loom'
}

version = "${mc_version}-${build_number}-FABRIC"
if (System.getenv().RELEASE_MODE != "1") {
    version += '-SNAPSHOT'
}

repositories {
	maven {
		name = "JEI"
		url "https://maven.blamejared.com/"
		content {
			includeGroup "mezz.jei"
		}
	}
	maven {
		name = "Modmuss"
		url = "https://maven.modmuss50.me/"
		content {
			includeGroup "me.zeroeightsix"
		}
	}
	maven {
		name = "Shedaniel"
		url = "https://maven.shedaniel.me/"
		content {
			includeGroup "me.shedaniel"
			includeGroup "me.shedaniel.cloth"
		}
	}
}

loom {
	mixin.defaultRefmapName = "patchouli.refmap.json"
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("../run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("../run")
		}
		gameTest {
			server()
			ideConfigGenerated(true)
			vmArg "-Dfabric-api.gametest=1"
		}
	}
}

dependencies {
	modImplementation "me.zeroeightsix:fiber:0.23.0-2"
	include "me.zeroeightsix:fiber:0.23.0-2"

	minecraft "com.mojang:minecraft:${mc_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:0.14.21"

	modImplementation "net.fabricmc.fabric-api:fabric-api:0.87.0+1.20.1"
    compileOnly project(":Xplat")

	modCompileOnly "mezz.jei:jei-1.20.1-common-api:15.2.0.27"

	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:10.0.578") { transitive = false }
	modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:9.0.94") { transitive = false }
}

compileJava {
	source(project(":Xplat").sourceSets.main.allSource)
}

sourcesJar {
	from project(":Xplat").sourceSets.main.allJava
}

processResources {
    from project(":Xplat").sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

/*
task apiJar(type: Jar, dependsOn: classes) {
	from(sourceSets.main.output)
	include 'vazkii/patchouli/api/**'

	archiveClassifier = "api"
}
*/

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version
			from components.java
		}
	}
	repositories {
		maven {
			url "file://" + System.getenv("local_maven")
		}
	}
}
